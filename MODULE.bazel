"""Build setup for chrome-linux-sysroot"""

module(name = "chrome-linux-sysroot", version = "0.0.1")

_BUILD_FILE = """
filegroup(
  name = "sysroot",
  srcs = glob(["*/**"]),
  visibility = ["//visibility:public"],
)
"""

# Based on https://github.com/chromium/chromium/blob/b79847bea1b8271c339e1b9d14ec23892b7ad14c/build/DEPS
SYSROOTS = {
    "arm64": "308e23faba3174bd01accfe358467b8a40fad4db4c49ef629da30219f65a275f",
    "amd64": "dec7a3a0fc5b83b909cba1b6d119077e0429a138eadef6bf5a0f2e03b1904631",
    "armhf": "fe81e7114b97440262bce004caf02c1514732e2fa7f99693b2836932ad1c4626",
    "i386": "b53933120bb08ffc38140a817e3f0f99782254a6bf9622271574fa004e8783a4",
    "mips64el": "783cb79f26736c69e8125788d95ffb65a28172349009d75188838a004280a92b",
    "mipsel": "fcf8c3931476dd097c58f2f5d44621c7090b135e85ab56885aa4b44f4bd6cdb5",
}

http_archive = use_repo_rule("@bazel_tools//tools/build_defs/repo:http.bzl", "http_archive")
[http_archive(
    name = "debian_bullseye_{}".format(arch),
    build_file_content = _BUILD_FILE,
    sha256 = sha256,
    type = "tar.xz",
    urls = ["https://commondatastorage.googleapis.com/chrome-linux-sysroot/{}".format(sha256)],
) for arch, sha256 in SYSROOTS.items()]
